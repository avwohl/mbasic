10 :REMARKEXPANDED APRIL 1977 BY W.A. BURTON
20 :REMARKPIRATED JAN. 1978 BY ZOSO
30 DIM G(8,8),S(8,8),K(3,3)
40 PRINT CHR$(26)
50 GOSUB 5460
60 PRINT
70 INPUT "WHAT IS YOUR SECURITY CLEARANCE NUMBER (1 TO 1000)";T9
80 E8=T9
90 IF T9<1  OR T9>1000  THEN  PRINT AR$;"INVALID!! REENTER!!":GOTO 70
100 FOR A=1  TO T9/2
110 R9=RND(1):R9=RND(2)
120 NEXT
130 CLEAR:PRINT CHR$(26):INPUT"       ENTER RANK     (1=LOW,12=HIGH)";R9
140 IF (R9<1  OR R9>12)  THEN  GOTO 130:RR=R9
150 X4=R9:R9=R9+0.001:W1=W1+0.001
160 PRINT  CHR$(26)
170 PRINT "COMMAND ORDER : STAND BY !"
180 PRINT "YOU ARE PRESENTLY BEING ASSIGNED TO A MISSION..."
190 PRINT " WITH A FAILURE FACTOR OF"; (RR*20)
200 PRINT"       GOOD-BYE, SIR..."
210 CX=0.017453:AR$=" ---> "
220 XA=INT(X4*50):AC=(1/(X4+0.1))
230 IFX4>=9THEN3800
240 E=3000-XA+100:P=10:S9=200
250 DEF  FND(D)=SQR((K(I,1)-S1)^2+(K(I,2)-S2)^2)
260 Q1=INT(RND(1)*8+1):Q2=INT(RND(1)*8+1)
270 S1=INT(RND(1)*8+1):S2=INT(RND(1)*8+1)
280 DATA WARP ENGINES,SHORT RANGE SENSORS,LONG RANGE SENSORS
290 DATA PHASERS, PHOTON TORPEDOES,SHIELD CONTROL
300 DATA DAMAGE CONTROL, COMPUTER,DAMAGE REPAIR,COMMUNICATIONS
310 FOR A=1TO10:READ D$(A):NEXT A
320 A$(0)=" . ":A$(1)=" E ":A$(2)=" * ":A$(3)=" K ":A$(4)=" B "
330 F1=0.86:F2=0.01:F3=0.95:F4=0.99:K9=0:B9=K9
340 FORI=1TO8:FORJ=1TO8:R1=RND(1):R2=RND(1):R3=INT(8*RND(1)+1)
350 K3=-(R1>F1-F2*R9)-(R1>F3-F2*R9)-(R1>F4-F2*R9):K9=K9+K3
360 B3=-(R2>F3):B9=B9+B3:G(I,J)=100*K3+10*B3+R3:NEXTJ,I
370 IF  NOT(B9>0ANDK9>0) THEN330
380 PRINT"OBJECTIVE:DESTROY"K9"KLINGONS USING"B9"STARBASE";
390 U=K9:Y=B9
400 IFB9=1THEN PRINT" ";:GOTO420
410 PRINT"S ";
420 T9=K9+RND(1)*K9/R9+10-R9:PR=T9/K9:PRINT"IN"T9"STARDAYS."
430 GOSUB 4260
440 S3=0:B3=S3:K3=B3:FORA=1TO8:FORB=1TO8:S(A,B)=0:NEXTB,A
450 FORA=1TO3:FORB=1TO3:K(A,B)=0:NEXTB,A:S(S1,S2)=1
460 X=0.01*G(Q1,Q2):K3=INT(X):Y=(X-K3)*10:B3=INT(Y)
470 S3=G(Q1,Q2)-100*K3-10*B3
480 IF K3=0THEN FORA=1TO3:FORB=1TO3:K(A,B)=0:NEXTB,A:GOTO520
490 PRINTAR$;"CONDITION RED !!!!!":FORA=1TOK3
500 R1=INT(RND(1)*8+1):R2=INT(RND(1)*8+1):IFS(R1,R2)<>0THEN500
510 S(R1,R2)=3:K(A,1)=R1:K(A,2)=R2:K(A,3)=S9:NEXT A:IFB3=0THEN550
520 IFB3=0THEN550
530 R1=INT(RND(1)*8+1):R2=INT(RND(1)*8+1):IFS(R1,R2)<>0THEN530
540 S(R1,R2)=4
550 IFS3=0THEN590
560 FORA=1TOS3
570 R1=INT(RND(1)*8+1):R2=INT(RND(1)*8+1):IFS(R1,R2)<>0THEN570
580 S(R1,R2)=2:NEXTA
590 G(Q1,Q2)=INT(G(Q1,Q2))+0.5
600 PRINT
610 PRINT"QUADRANT (";MID$(STR$(Q1),2,1);",";MID$(STR$(Q2),2,1);")"
620 PRINT"SECTOR   (";MID$(STR$(S1),2,1);",";MID$(STR$(S2),2,1);")"
630 IFKP<>0THEN4050
640 IF DT=1  THEN 2680
650 IFS<200*K3 THEN PRINTAR$;"SHIELD ENERGY TOO LOW":SL=1:A=6:GOTO750
660 A=2:  GOTO750
670 PRINT
680 PRINTD$(2):FORA=1TO8:FORB=1TO8:PRINTA$(S(A,B));:NEXTB:PRINT:NEXTA
690 PRINT:PRINT"SHIELDS";S;SPC(4);"ENERGY";E;SPC(4);"PHOTONS";P;
700 PRINT SPC(4);"STARDAYS";T9-T
710 GOSUB3220:INPUT"COMMAND";A
720 IF A>10  OR A<0  THEN  INPUT" ----> COMMAND";A
730 IFA>10ORA<0THEN PRINT"WARNING - PROPER COMMANDS ONLY !! ":GOTO 710
740 IFA=1THEN780
750 IFD(A)>=0THEN780
760 PRINTAR$;"CAPTAIN, WE DON'T HAVE ";D$(A);" ANYMORE."
770 IF DT=1  THEN 2680 :ELSE 710
780 ONA GOTO820,670,1470,1530,1790,2140,2230,2680,4900,3900
790 PRINT:FORA=1TO9:PRINTA;" = ";D$(A):NEXTA
800 A=10
810 PRINTA;"= ";D$(A):GOTO710
820 INPUT"COURSE";C1:IF C1<0  OR C1>1.40621  THEN850
830 INPUT"WARP FACTOR";W1
840 SL=0:KP=0:GOTO 860
850 PRINTAR$"REJECTED ! COURSE MUST BE IN RANGE OF O TO 359.99 DGRS."
860 IF NOT(W1>0) THEN710
870 IFD(1)>=0ORW1<=AC THEN900
880 PRINTAR$;"WARP ENGINES ARE DAMAGED, MAXIMUM SPEED = ";AC
890 GOTO820
900 TEC=TEC+1:C$="":IFK3>0THEN GOSUB2290
910 IFE>5*W1 THEN980
920 IFS<1THEN2450
930 PRINT"CAPTAIN, YOU ONLY HAVE"E"UNITS OF ENERGY. "
940 PRINT"REFUEL FROM YOUR SHIELD RESERVES, WHICH HAVE"S"UNITS";
950 Q9$="NO":INPUT " ";Q9$:IF (Q9$="Y"  OR Q9$="YES")  THEN  GOSUB 6340
960 A=6:IFD(A)<0THEN2450
970 GOTO710
980 FORI=1TO10:IFD(I)>=0THEN1020
990 D(I)=D(I)+1:IFD(I)<0THEN1020
1000 IFD(7)<0THEN1020
1010 PRINT"DAMAGE CONTROL REPORT: "D$(I)" REPAIRED."
1020 NEXTI:IF RND(1)>0.1THEN1130
1030 IF RND(1)>0.1THEN1130
1040 GOTO1100
1050 IF RND(1)>R9/10THEN RETURN
1060 R1=INT(RND(1)*8+1):IFD(R1)<0THEN RETURN
1070 D(R1)=D(R1)-10*RND(1)-1:IFD(7)<0THEN RETURN
1080 IFD(7)<0THEN RETURN
1090 PRINT:PRINT"DAMAGE CONTROL REPORT: "D$(R1)" OUT.":PRINT:RETURN
1100 R1=INT(RND(1)*8+1):IFD(R1)>=0THEN1130
1110 IFD(7)<0THEN1130
1120 PRINT:D(R1)=0:PRINT"DAMAGE CONTROL REPORT: "D$(R1)" REPAIRED.":PRINT
1130 W1=W1*8:A1=8*Q2+S2-9:B1=72-8*Q1-S1:IFW1<1THENW1=W1*1.25
1140 E=E-2*W1:T=T+W1/25:IFT>T9 THEN2520
1150 A2=INT(A1+W1*COS(C1*CX)+0.5)
1160 B2=INT(B1+W1*SIN(C1*CX)+0.5)
1170 IF NOT(A2<0ORA2>63ORB2<0ORB2>63) THEN1200
1180 PRINTAR$;"CAPTAIN, HEED STARFLEET REGULATIONS! ( STAY IN GALAXY ) !!
1190 T=T+W1/24:GOTO710
1200 DEF  FNA1(X)=INT(0.5+A1+X*COS(C1*CX))
1210 DEF  FNB1(X)=INT(0.5+B1+X*SIN(C1*CX))
1220 FORX=0TO INT(W1)
1230 IF NOT(Q1=8-INT(FNB1(X)/8) ANDQ2=INT(FNA1(X)/8+1)) THENX=W1:GOTO1400
1240 S3=8-FNB1(X)+8*INT(FNB1(X)/8):S4=FNA1(X)+1-8*INT(FNA1(X)/8)
1250 IFS(S3,S4)<2THEN1400
1260 S(S1,S2)=0:S1=8-FNB1(X-1)+8*INT(FNB1(X-1)/8)
1270 S2=FNA1(X-1)+1-8*INT(FNA1(X 1)/8):S(S1,S2)=1
1280 PRINTAR$;"NAVIGATIONAL ERROR :ENGINES SHUT DOWN AT (";
1290 PRINT MID$(STR$(S1),2,1);"," MID$(STR$(S2),2,1);")"
1300 T=T+(W1/24)
1310 PRINT" YOU LOST "T" UNITS STARTIME RESTARTING ENGINES."
1320 PRINT
1330 IF S(S3,S4)<>4  THEN 710
1340 PRINT"SHIELDS LOWERED FOR REFUELING"
1350 S=0:P=10:C$="D":E=3000-XA+100
1360 GOSUB 2290
1370 FORR1=1TO 10
1380 IFD(R1)>=0THEN NEXTR1:GOTO710
1390 D(R1)=D(R1)+(11*RND(1)/R9):NEXTR1:GOTO710
1400 NEXT X:S(S1,S2)=0:S1=8-B2+8*INT(B2/8):S2=A2+1-8*INT(A2/8)
1410 Q3=8-INT(B2/8):Q4=INT(A2/8)+1
1420 IF NOT(Q1=Q3 ANDQ2=Q4) THENQ1=Q3:Q2=Q4:GOTO440
1430 S(S1,S2)=1:FORA=S1-1TOS1+1:FORB=S2-1TOS2+1
1440 IFA>8ORB>8ORA<1ORB<1THEN1460
1450 IFS(A,B)=4THEN1340
1460 NEXTB,A:GOTO710
1470 PRINT
1480 FOR A=(Q1-1)  TO (Q1+1):FOR B=(Q2-1)  TO (Q2+1)
1490 IFA<1ORB<1ORA>8ORB>8THEN PRINT" ***";:NEXTB:PRINT" ":PRINT:NEXTA:GOTO710
1500 PRINT" " SPC(3-LOG(G(A,B)+1)/LOG(10));
1510 PRINT MID$(STR$(G(A,B)),2,LOG(G(A,B))/LOG(10)+1);
1520 G(A,B)=INT(G(A,B))+0.5:NEXTB:PRINT" ":PRINT:NEXTA:GOTO710
1530 PRINT
1540 IFK3<=0THEN2270
1550 IFD(8)>=0THEN1570
1560 PRINTAR$;" COMPUTER FAILURE HAMPERS ACCURACY"
1570 PRINT"PHASERS LOCKED ON TARGET.  ENERGY AVAILABLE="E
1580 PRINT"NUMBER OF UNITS TO FIRE:";
1590 INPUT X
1600 IF E-X<0THEN PRINT"NOT ENOUGH POWER":GOTO710
1610 T=T+0.05:IFT>T9 THEN2520
1620 E=E-X
1630 IF C$<>"D" THEN  GOSUB 2290
1640 IFD(8)>=0THEN1660
1650 X=X*RND(1)
1660 FORI=1TO3
1670 IFK(I,3)<=0THEN1770
1680 H=(X/FND(0))+SGN(RND(1)-0.5)*8*RND(1)
1690 K(I,3)=K(I,3)-H
1700 PRINTH"UNIT HIT ON VESSEL AT (";MID$(STR$(K(I,1)),2,1);",";
1710 PRINT MID$(STR$(K(I,2)),2,1);"), LEAVING"K(I,3)"
1720 IFK(I,3)>0THEN1770
1730 PRINT"KLINGON AT (";MID$(STR$(K(I,1)),2,1);",";MID$(STR$(K(I,2)),2,1);
1740 PRINT") DESTROYED":K3=K3-1:K9=K9-1:IFK9=0THEN2650
1750 G(Q1,Q2)=G(Q1,Q2)-100:S(K(I,1),K(I,2))=0:K(I,1)=0:K(I,2)=0
1760 K(I,3)=0
1770 NEXT I
1780 GOTO710
1790 PRINT
1800 IFP>0THEN1830
1810 PRINTAR$;"ALL PHOTON TORPEDOES EXPENDED"
1820 GOTO710
1830 INPUT"DIRECTION";C1
1840 T=T+0.05:IFT>T9 THEN2520
1850 P=P-1:IF C$<>"D"  THEN  GOSUB 2290
1860 A1=8*Q2+S2-9:B1=72-8*Q1-S1:IFC1=90*INT(C1/90) THEN2090
1870 DEF  FNA1(W1)=INT(0.5+A1+W1*COS(C1*CX))
1880 DEF  FNB1(W1)=INT(0.5+B1+W1*SIN(C1*CX))
1890 FORW1=0TO10 STEP0.9
1900 IFW1=0THENS3=S1:S4=S2:GOTO1950
1910 IF NOT(Q1=8-INT(FNB1(W1)/8) ANDQ2=INT(FNA1(W1)/8+1)) THEN710
1920 S3=8-FNB1(W1)+8*INT(FNB1(W1)/8):S4=FNA1(W1)+1-8*INT(FNA1(W1)/8)
1930 IF FNA1(W1)=A2 AND FNB1(W1)=B2 THEN2070
1940 A2=FNA1(W1):B2=FNB1(W1)
1950 PRINT"(";MID$(STR$(S3),2,1);",";MID$(STR$(S4),2,1);")";A$(S(S3,S4))
1960 ON S(S3,S4)+1GOTO2070,2070,2050,1970,2040
1970 IF RND(1)<(0.1*X4)  THEN PRINT"SHIELDS DEFLECT TORPEDO":GOTO710
1980 PRINT"KLINGON DESTROYED":S(S3,S4)=0:G(Q1,Q2)=G(Q1,Q2)-100
1990 K3=K3-1:K9=K9-1:IFK9=0THEN2650
2000 FORA=1TO3
2010 IFK(A,1)=S3 ANDK(A,2)=S4 THENK(A,1)=0:K(A,2)=0:K(A,3)=0
2020 NEXTA
2030 GOTO710
2040 PRINTAR$;"STARBASE DESTROYED":B3=0:B9=B9-1:S(S3,S4)=0:GOTO2060
2050 PRINTAR$;"YOU CAN'T DESTROY A STAR":GOTO710
2060 G(Q1,Q2)=G(Q1,Q2)-10:GOTO710
2070 NEXT W1
2080 GOTO710
2090 FORW1=0TO8
2100 S3=INT(S1-W1*SIN(C1*CX))
2110 S4=INT(S2+W1*COS(C1*CX))
2120 IFS3>8ORS4>8ORS3<1ORS4<1THEN710
2130 GOTO1950
2140 PRINT
2150 PRINT"ENERGY AVAILABLE="E+S"    NUMBER OF UNITS TO SHIELDS";
2160 INPUT X
2170 IFX<0THEN710
2180 IFE+S-X<0THEN2150
2190 E=E+S-X
2200 S=X
2210 IFSL=1THEN660
2220 GOTO710
2230 PRINT
2240 PRINT"DEVICE        STATE OF REPAIR"
2250 FORR1=1TO10:PRINTD$(R1);TAB(21) D(R1):NEXTR1
2260 IF DT=1  THEN 2680 :ELSE 710
2270 PRINT"SHORT RANGE SENSORS REPORT NO KLINGONS IN THIS QUADRANT"
2280 IF DT=1  THEN 2680 :ELSE 710
2290 IF C$="D"  AND K3<>0  THEN  PRINT"STARBASE PROTECTS ENTERPRISE" :ELSE 2310
2300 GOTO710
2310 PRINT
2320 FORI=1TO3
2330 IFK(I,3)<=0THEN2430
2340 H=K(I,3)/FND(0)+SGN(RND(1)-0.5)*RND(1)*7
2350 S=S-H:PRINTH"UNIT HIT FROM (" MID$(STR$(K(I,1)),2,1);",";
2360 PRINT MID$(STR$(K(I,2)),2,1);"), LEAVING"S
2370 IFS<0THEN2550
2380 GOSUB1050
2390 IF RND(1)>R9/10THEN2430
2400 R1=INT(8*RND(1)+1):R2=INT(8*RND(1)+1)
2410 IF NOT(S(R1,R2)=0) THEN2400
2420 S(R1,R2)=3:S(K(I,1),K(I,2))=0:K(I,1)=R1:K(I,2)=R2
2430 NEXTI
2440 RETURN
2450 PRINT AR$;" ENTERPRISE DEAD IN SPACE"
2460 PRINT
2470 CY=1:PRINT:GOTO2590
2480 IFK3<=0THEN2590
2490 GOSUB2290
2500 GOTO2480
2510 PRINT
2520 PRINT:PRINT:PRINT:PRINT:PRINT:PRINTAR$;
2530 PRINT"IT IS STARDATE"T:PRINT
2540 GOTO2590
2550 PRINT
2560 PRINT:PRINT:PRINT:PRINT:PRINT:PRINTAR$;
2570 PRINT"ENTERPRISE DISABLED !!":PRINT:PRINT
2580 CY=0
2590 PRINT"THERE ARE"K9"KLINGONS REMAINING.":IFCY<>0THEN3560
2600 PRINT"YOU HAVE FAILED !!":PRINT
2610 L=INT(RND(1)*5)
2620 ONL+1GOTO 3440,3470,3490,3510,3540
2630 PRINT:INPUT"DO YOU WANT TO TRY AGAIN";X$
2640 IF  LEFT$(X$,1)="Y"  THEN 130 :ELSE 5420
2650 PRINT"THE FEDERATION IS SAVED !!"
2660 PRINT"YOU ARE HEREBY PROMOTED TO ADMIRAL!!!!":PRINT
2670 GOTO2630
2680 DT=1
2690 INPUT"COMPUTER ON--COMMAND";A:  IF A=0  THEN  GOTO 2720
2700 IF A<0  OR A>=10  THEN  PRINT AR$;" NO SUCH COMMAND !!":GOTO 2690
2710 ONA  GOTO2900,2950,3000,2840,600,3400,3420,5110,710
2720 PRINT"FUNCTIONS AVAILABLE FROM COMPUTER"
2730 PRINT"   1 = GALACTIC MEMORY MAP"
2740 PRINT"   2 = GENERAL STATUS REPORT"
2750 PRINT"   3 = PHOTON TRAJECTORY"
2760 PRINT"   4 = NEW MISSION"
2770 PRINT"   5 = PRESENT POSITION"
2780 PRINT"   6 = SELF-DESTRUCT"
2790 PRINT"   7 = WARP-COMPASS"
2800 PRINT"   8 = MISSION PROGRESS REPORT"
2810 PRINT"   9 = EXIT COMPUTER"
2820 GOTO2680
2830 PRINT:PRINT"--TEMPORARY MALFUNCTION--":GOTO 710
2840 IF XM<0  THEN 2880
2850 PRINT AR$;"SAFETY CHECK -- DO YOU WISH TO ABORT MISSION":INPUT AN$
2860 IF  LEFT$(AN$,1)<>"Y"  THEN 2890 :ELSE 130
2870 PRINT
2880 PRINT"YOU HAVE NOT BEEN AUTHORIZED FOR NEW MISSION !!":PRINT
2890 GOTO 2680
2900 PRINT"QUADRANT CODE MEMORY MAP"
2910 FORA=1TO8:FORB=1TO8:IFG(A,B)=INT(G(A,B)) THEN PRINT" ???";:GOTO2940
2920 PRINT" " SPC(3-LOG(G(A,B)+1)/LOG(10));
2930 PRINT MID$(STR$(G(A,B)),2,LOG(G(A,B))/LOG(10)+1);
2940 NEXTB:PRINT" ":NEXTA:GOTO 2680
2950 PRINT"   STATUS REPORT"
2960 PRINT"NUMBER OF KLINGONS LEFT ="K9
2970 PRINT"NUMBER OF STARDATES LEFT ="T9-T
2980 PRINT"NUMBER OF STARBASES LEFT ="B9
2990 A=7:GOTO750
3000 IFK3=0THEN2270
3010 IFD(2)<0THENA=2:GOTO740
3020 PRINT"COORD","LOWDIR","HIGHDIR","DIST"
3030 FORA=1TO47:PRINT"-";:NEXTA
3040 PRINT
3050 FORI=0TO3
3060 IF NOT(K(I,3)>0) THEN3190
3070 W1=SQR((S1-K(I,1))^2+(S2-K(I,2))^2)
3080 IF W1=0  THEN PRINT"--->MALFUNCTION !":GOTO710
3090 ON  ERROR  GOTO 2830
3100 C1=1.5708-ATN((K(I,2)-S2)/W1/SQR(1-((K(I,2)-S2)/W1)^2))
3110 ON  ERROR  GOTO 2830
3120 C1=C1*SGN(S1-K(I,1))/CX
3130 C2(I)=C1-(10*RND(1))
3140 C3(I)=C1+(10*RND(1))
3150 IF C3(I)<0  THEN C3(I)=C3(I)+360
3160 IF C2(I)<0  THEN C2(I)=C2(I)+360
3170 PRINT"(";MID$(STR$(K(I,1)),2,1);",";MID$(STR$(K(I,2)),2,1);
3180 PRINT")",C2(I),C3(I),W1
3190 NEXTI
3200 IF DT=1  THEN 2680 :ELSE 710
3210 RETURN
3220 MR=(T9-T)/K9:DT=0
3230 XM=((MR-PR)/PR)*100
3240 IFKA=1THEN PRINTTK-T"STARDATES LEFT TO SAVE STARBASE.":GOTO3330
3250 IF RND(1)>0.01*R9 ORB9=0THEN RETURN
3260 KA=1:FORA=1TO8:FORB=1TO8
3270 IFG(A,B)-100*INT(G(A,B)/100)>9THENK1=A:K2=B:IF RND(1)>0.5THENA=8:B=8
3280 NEXTB,A:TK=T+0.09*SQR((Q1-K1)^2+(Q2-K2)^2)*(10-R9)
3290 TK=TK+1
3300 PRINTAR$;"!!! STARBASE IN QUADRANT (" MID$(STR$(K1),2,1)",";
3310 PRINT MID$(STR$(K2),2,1)") IS UNDER ATTACK!!"
3320 PRINT"YOU HAVE"TK-T"STARDATES TO SAVE IT!":RETURN
3330 IFT<TK THEN3380
3340 PRINT"TOO LATE!":KA=0:G(K1,K2)=G(K1,K2)-10:B9=B9-1
3350 IF NOT(Q1=K1 ANDQ2=K2) THEN RETURN
3360 FORA=1TO8:FORB=1TO8:IFS(A,B)=4THENS(A,B)=0
3370 NEXTB,A:RETURN
3380 IFQ1=K1 ANDQ2=K2 THEN PRINT"YOU ARRIVED IN TIME! STARBASE SAVED!"
3390 KA=0:GOTO710
3400 IF SD<>1  THEN 4220 :ELSE 5310
3410 GOTO710
3420 GOSUB 3660
3430 GOTO 2680
3440 PRINT"YOU HAVE SCREWED UP ONCE TOO OFTEN !"
3450 PRINT"OFF TO THE VULCAN TORTURE CAMPS !!"
3460 IFT<= 0 THEN2630:ELSE3560
3470 PRINT"YOU WILL BE EXECUTED AT SUNRISE"
3480 GOTO3560
3490 PRINT"YOU WILL BE PUT TO DEATH FOR YOUR INCOMPETENCE"
3500 GOTO3560
3510 PRINT"YOU WILL BE CONFINED TO YOUR QUARTERS UNTIL"
3520 PRINT"VULCAN'S MOONS BECOME TOURIST TRAPS !!"
3530 IFT<=0THEN2630:ELSE3560
3540 PRINT"IMBECILE !! WE HOPE YOU CONSIDER SUICIDE!"
3550 IFT<=0THEN2630:ELSE3560
3560 L=(U-K9)*10+((U-K9)*500/T)-100*(Y-B9)
3570 IFE<=0ORS<0THENL=L-200
3580 IFK9=0THENL=L+(R9*100)
3590 PRINT:PRINT
3600 PRINT"YOUR MISSION RATING IS: ";L
3610 PRINT:PRINT:PRINT:PRINT:PRINT
3620 GOSUB 5440
3630 PRINT CHR$(26)
3640 GOTO5450
3650 GOTO2630
3660 PRINT
3670 PRINT"             O9O"
3680 PRINT"  135         .        O45"
3690 PRINT"       .      .      ."
3700 PRINT"          .   .   ."
3710 PRINT"18O .  .  .   .   .  .  .OOO       WARP-COMPASS"
3720 PRINT"          .   .   ."
3730 PRINT"       .      .      ."
3740 PRINT"  225         .        315"
3750 PRINT"             27O"
3760 PRINT
3770 RETURN
3780 XX=INT(RND(2)*10)+1
3790 RETURN
3800 PRINT:IFX4>12THEN3890
3810 PRINT"COMMAND CENTRAL ADVISES THAT YOUR RANKING-";X4;"-IS IN THE EXPERT"
3820 PRINT"CATEGORY. BE ADVISED THAT IF YOU ARE NOT PROPERLY QUALIFIED"
3830 PRINT"YOUR CHANCES OF AVOIDING FAILURE ARE NIL."
3840 PRINT
3850 PRINT"DO YOU WISH REASSIGNMENT TO A LESS HAZARDOUS MISSION (Y OR N)"
3860 INPUTAN$
3870 IF LEFT$(AN$,1)="Y" THEN130
3880 PRINT CHR$(26):GOTO240
3890 PRINT"YOU HAVE REQUESTED A SUICIDE MISSION":GOTO3840
3900 PRINT"--COMMUNICATIONS ACTIVE--"
3910 PRINT
3920 GOSUB3780
3930 PD=XX
3940 GOSUB3780
3950 PE=XX
3960 GOSUB3780
3970 PF=XX
3980 GOSUB3780
3990 PG=XX
4000 PH=PD*10+PE
4010 PJ=PF*10+PG
4020 IFPH=PJ THEN 2830
4030 KP=1:IFPD=PE ORPF=PG THEN4040:ELSE4060
4040 PRINT"SUNSPOTS BLOCK TRANSMISSION AT : ":GOTO610
4050 PRINT"MOVE ELSEWHERE AND TRY AGAIN":PRINT:GOTO 820
4060 PRINT"AUTHORIZED FREQUENCIES":PRINT
4070 PRINT"FEDERATION COMMAND BASE = ";PH
4080 PRINT"KLINGON COMMAND CENTRAL = ";PJ
4090 PRINT
4100 INPUT"COMMUNICATION ON CHANNEL ";XF
4110 IFXF=PH THEN4330
4120 IFXF=PJ THEN4620
4130 PRINT
4140 PRINTAR$;"ALERT !!"
4150 PRINT"ATTEMPTED COMMUNICATION ON UNAUTHORIZED FREQUENCY"
4160 PRINT
4170 PRINT"WHILE SECURITY CHECK IS CONDUCTED ON ENTERPRISE"
4180 GOSUB3780:A=XX
4190 GOSUB3780:I=(XX/2)+(X4/7)
4200 PRINT"YOUR ";D$(A);" WILL BE INOPERATIVE FOR ";I "UNITS OF STARTIME"
4210 D(A)=D(A)-I:GOTO710
4220 PRINT
4230 PRINT"SECURITY CONTROL ADVISES THAT NO AUTHORIZATION HAS BEEN"
4240 PRINT"GIVEN TO ACTIVATE SELF- DESTRUCT SYSTEM."
4250 PRINT"PROCEED WITH ASSIGNED MISSION":PRINT:GOTO 2690
4260 PRINT
4270 PRINT"BE ADVISED, YOUR MISSION PROGRESS FACTOR IS";PR
4280 GOSUB 3780
4290 TF=(XX*X4)/1.6:IF TF<10  THEN TF=TF+10
4300 PRINT" THE TOLERANCE FACTOR FOR THIS MISSION IS [+/-]";TF;"PERCENT"
4310 PRINT
4320 RETURN
4330 ON SGN(XM)+2GOTO4340,4380,4400
4340 IF ABS(XM)>(TF*1.5) THEN4460
4350 IF ABS(XM)>TF THEN4430
4360 PRINT:PRINT"CAPTAIN, YOU ARE BEHIND SCHEDULE - LET'S GO !!"
4370 GOTO 710
4380 PRINT:PRINT"YOU ARE EXACTLY ON COMPUTED SCHEDULE; PROCEED WITH MISSION"
4390 GOTO710
4400 IFXM>TF THEN4550:IFXM>(TF*1.5) THEN4580
4410 PRINT:PRINT"YOU ARE AHEAD OF COMPUTED SCHEDULE, KEEP UP THE GOOD WORK !!"
4420 GOTO710
4430 PRINT:PRINT"YOU ARE BEHIND SCHEDULE AND OUT OF TOLERANCE FACTOR !!"
4440 PRINT"COMMAND CONTROL PROJECTS FAILURE...YOU MAY SURRENDER TO ENEMY"
4450 EQ=1:SD=1:GOTO 3900
4460 YY=AC^X4
4470 PRINT CHR$(26)
4480 PRINTAR$;"---------COMPUTER PROJECTION----------":PRINT
4490 PRINT"PROJECTED LIKELIHOOD OF SUCCESS FOR YOUR MISSION =";YY;"PERCENT"
4500 PRINT"WE HAVE AUTO ACTIVATED SELF-DESTRUCT SYSTEM ON YOUR VESSEL"
4510 PRINT"GOODBYE, CAPTAIN.............":PRINT
4520 PRINT"END OF MESSAGE FROM FEDERATION COMMAND":PRINT:PRINT
4530 GOSUB 5440
4540 GOTO 5310
4550 PRINT:PRINT"YOU ARE AHEAD OF SCHEDULE AND BEYOND TOLERANCE FACTOR"
4560 PRINT"CALL KLINGON LEADERS AND REQUEST THEIR SURRENDER":KQ=1
4570 PRINT:GOTO3900
4580 PRINTAR$;"MESSAGE FROM COMMAND BASE"
4590 PRINT:PRINT"ENEMY HAS SURRENDERED TO FEDERATION COMMAND !!"
4600 PRINT:PRINT:PRINT:PRINT
4610 GOTO2650
4620 IF KQ=1  THEN 4870:IF EQ=1  THEN 4650
4630 ON  SGN(XM)+2  GOTO 4640,4700,4750
4640 IF  ABS(XM)<=TF THEN4700
4650 PRINT:PRINT"KLINGON COMMAND ON FREQUENCY"
4660 PRINT"NATURALLY, WE ARE PLEASED THAT YOU ARE CONCEDING, CAPTAIN"
4670 PRINT"WE ARE PRESENTLY BOARDING YOUR CRAFT"
4680 PRINT:PRINT"HERE IS A MESSAGE FROM YOUR LEADERS-":PRINT:GOSUB 5440
4690 PRINT CHR$(26):GOTO 2610
4700 GOSUB 3780
4710 IFXX<=5THEN 4740
4720 PRINT:PRINT"MESSAGE FROM KLINGON COMMAND...."
4730 PRINT"NO FURTHER COMMUNICATIONS AT THIS TIME":GOTO 4830
4740 PRINT:PRINT"WE'LL TALK WITH OUR WEAPONS, EARTH-SWINE !!":GOTO 4830
4750 GOSUB 3780
4760 AQ=INT((XX+1)/2)
4770 ON AQ  GOTO 4720,4740,4780,4780,4750
4780 GF=TF*1.5
4790 IFXM>GF THEN 4870
4800 IFXM>TF THEN 4840
4810 PRINT:PRINT"CAPTAIN, DESPITE YOUR SLIGHT ADVANTAGE, YOU ARE NO MATCH FOR"
4820 PRINT"THE GLORIOUS KLINGON FLEET"
4830 PRINT TAB(25)"END OF MESSAGE FROM KLINGON COMMAND":GOTO710
4840 GOSUB3780
4850 IFXX<=5THEN PRINT" KLINGON COMMAND TO ENTERPRISE..":GOTO4740
4860 GOTO4880
4870 PRINT CHR$(26)
4880 PRINT:PRINT"MESSAGE FROM KLINGON LEADERS..."
4890 PRINT:PRINT"WE CONCEDE THE WAR, SIR, CONGRATULATIONS !!":GOTO3610
4900 AV=INT(X4/2)+1:DR=-1
4910 IF TEC<AV  THEN 4930
4920 IF C$<>"D"  THEN 4970 :ELSE 4990
4930 AU=AV-TEC:IFAU=1THENKF$="":ELSEKF$="S"
4940 PRINT:PRINT"DAMAGE REPAIR INACTIVE !!"
4950 PRINT"REQUIRES";AU;"MORE RECHARGE UNIT";KF$
4960 PRINT:GOTO 710
4970 PRINT:PRINTAR$"YOU MUST BE DOCKED AT STARBASE FOR DAMAGE REPAIR !!"
4980 PRINT:GOTO 710
4990 PRINT
5000 PRINT:FORA=1TO10
5010 IFD(A)<0THEN PRINT  TAB(10)A;TAB(20)D$(A);TAB(40)"DAMAGED"
5020 NEXT
5030 PRINT:PRINT"IF NO DAMAGE INDICATED ABOVE, ANSWER WITH 0 (ZERO)"
5040 INPUT "WHICH ONE TO REPAIR ";A
5050 IFA<1ORA>10THEN PRINTAR$;:GOTO 710
5060 IF D(A)<0  THEN 5080
5070 PRINT:PRINTD$(A);"        NOT DAMAGED !!":GOTO 5040
5080 PRINT:PRINT TAB(20)D$(A);"---REPAIRED":PRINT
5090 D(A)=0:TEC=0:GOTO710
5100 PRINT:PRINTAR$;"NOTHING DAMAGED AT PRESENT":GOTO710
5110 PRINT
5120 GOSUB4300
5130 PRINT"INITIAL MISSION PROGRESS FACTOR WAS";PR
5140 PRINT"CURRENT MISSION PROGRESS FACTOR IS ";MR
5150 IF XM<0  THEN PW$=" WORSENED ":ELSE PW$=" IMPROVED "
5160 PRINT
5170 PRINT"YOUR COMBAT SITUATION HAS";PW$;"BY A FACTOR OF";XM;"PERCENT"
5180 IF  ABS(XM)>=TF  THEN EG$=" NOT ":ELSE EG$=" "
5190 IF EG$=" " THEN EH$="":ELSE EH$=AR$
5200 PRINT EH$;"YOUR PROGRESS IS";EG$;"WITHIN TOLERANCE FOR THIS MISSION"
5210 PRINT
5220 IF EG$=" NOT " THEN 5230 :ELSE 2680
5230 IF XM<0  THEN 5240 :ELSE 5280
5240 PRINT:SD=1:KD=0
5250 PRINT"CAPTAIN, COMMAND CENTRAL PROJECTS DEFEAT BY ENEMY. YOU ARE NOW"
5260 PRINT"AUTHORIZED TO SURRENDER OR TO ACTIVATE SELF-DESTRUCT SYSTEM"
5270 GOTO2680
5280 PRINT:SD=0:KD=1
5290 PRINT"CAPTAIN, FEDERATION COMMAND PROJECTS SUCCESS FOR YOUR MISSION."
5300 GOTO 4550
5310 PRINT CHR$(26)
5320 GOSUB 5430
5330 PRINT"SELF DESTRUCT SYSTEM ACTIVATED"
5340 PRINT:PRINT:PRINT:GOSUB 5430
5350 PRINT"COUNTDOWN BEGUN !":PRINT:PRINT
5360 PRINT TAB(25)"FIVE":PRINT:GOSUB 5430
5370 PRINT TAB(20)"FOUR":PRINT:GOSUB 5430
5380 PRINT TAB(15)"THREE":PRINT:GOSUB 5430
5390 PRINT TAB(10)"TWO":PRINT:GOSUB 5430
5400 PRINT TAB(5)"ONE":PRINT:GOSUB 5430
5410 PRINT"ZERO":GOSUB 5430
5420 PRINT CHR$(26):GOTO5450
5430 FOR I=1  TO 50:A=A+1:NEXT:RETURN
5440 FOR I=1  TO 1200:A=A+1:NEXT:RETURN
5450 GOTO 6390
5460 REM INTRODUCTION TO GAME. R.SCHRADER, 1/12/83
5470 PRINT:PRINT:PRINT:PRINT
5480 PRINT "                    SPACE - THE FINAL FRONTIER!":PRINT
5490 PRINT "    THESE ARE THE VOYAGES OF THE STARSHIP ENTERPISE.  THE MISSION: "
5500 PRINT "    TO SEEK NEW LIFE AND NEW CIVILIZATIONS; TO BOLDLY GO WHERE NO"
5510 PRINT "                      MAN HAS GONE BEFORE.":PRINT:PRINT:PRINT:PRINT
5520 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT:INPUT "DO YOU NEED INSTRUCTIONS";Q9$
5530 IF (Q9$ = "NO")  THEN  RETURN :ELSE  PRINT CHR$(26) 
5540 PRINT:PRINT:PRINT:PRINT:
5550 PRINT "        **********    WELCOME TO STARTREK    **********":PRINT
5560 PRINT "    YOU ARE IN CONTROL OF THE STARSHIP ENTRPRISE. YOUR MISSION"
5570 PRINT "WILL BE TO RID THE GALAXY OF KLINGONS."
5580 PRINT "    THE KNOWN GALAXY IS MAPPED INTO 64 QUADRANTS (8 X 8). EACH"
5590 PRINT "QUADRANT IS COMPOSED OF 64 SECTORS (ALSO 8 X 8)."
5600 PRINT "    A MAP OF THE GALAXY, AS REPRESENTED BY YOUR ON-BOARD COM-"
5610 PRINT "PUTER, LOOKS LIKE THIS:"
5620 FOR Q8=1TO3:PRINT "      ??? ??? ??? ??? ??? ??? ??? ???":NEXT Q8
5630              PRINT "      ??? ??? ??? ??? 316 ??? ??? ???"
5640 FOR Q8=1TO4:PRINT "      ??? ??? ??? ??? ??? ??? ??? ???":NEXT Q8  
5650 PRINT "THE QUESTION MARKS REPRESENT UNEXPLORED QUADRANTS. THE 3-DIGIT"
5660 PRINT "NUMBERS ENCODE INFORMATION ABOUT QUADRANTS. THE FIRST DIGIT"
5670 PRINT "TELLS HOW MANY KLINGONS ARE IN THE QUADRANT; THE SECOND, HOW"
5680 PRINT "MANY FRIENDLY BASES; THE THIRD, HOW MANY STARS."
5685 PRINT:INPUT "HIT RETURN TO CONTINUE";Q8:PRINT:PRINT
5690 PRINT "    WHEN YOU ARE IN A QUADRANT, IT WILL BE DISPLAYED AS:"
5700 PRINT "     . . . . . . . ."
5710 PRINT "     . K . . . . * ."
5720 PRINT "     . . . . . . . ."
5730 PRINT "     * . . * . . . ."
5740 PRINT "     . . . . . E . ."
5750 PRINT "     . . . K . . . B"
5760 PRINT "     . * K . . . . ."
5770 PRINT "     . . . * . . . *"
5780 PRINT "THE KLINGONS ARE REPRESENTED BY 'K', STARS BY '*', YOUR SHIP BY"
5790 PRINT "AN 'E', BASES BY 'B', AND EMPTY SECTORS BY '.'"
5795 PRINT:INPUT "HIT RETURN TO CONTINUE";Q8:PRINT:PRINT
5800 PRINT "    IN YOUR PURSUIT OF KLINGONS, YOU WILL HAVE A LARGE NUMBER OF"
5810 PRINT "UNITS OF ENERGY. THESE CAN BY USED TO MOVE THE SHIP, TO DEFEND"
5820 PRINT "THE SHIP, OR TO FIRE AT KLINGONS. IN EXECUTING ANY OF THE AFORE-"
5830 PRINT "MENTIONED FUNCTIONS, ENERGY IS EXPENDED. IF YOU EVER RUN OUT OF"
5840 PRINT "ENERGY, YOU WILL BE IMMOBILE AND DEFENSELESS."
5850 PRINT "    A SUMMARY OF COMMANDS FOLLOWS:"
5860 PRINT
5870 PRINT "**WARP  (ACTIVATED BY ENTERING A '1' IN RESPONSE TO 'COMMAND?')"
5880 PRINT "    'WARP' MOVES THE STARSHIP. UPON ENTERING THE COMMAND, YOU"
5890 PRINT "WILL BE ASKED FOR A DIRECTION OF TRAVEL ( 0 TO 360 DEGREES,"
5900 PRINT "0 IS RIGHT, 90 IS UP, ETC), AND THE WARP SPEED."
5910 PRINT 
5920 PRINT "**SHORT-RANGE SCAN  (ACTIVATED BY A '2')"
5930 PRINT "    DISPLAYS THE LOCAL QUADRANT."
5935 PRINT:INPUT "HIT RETURN TO CONTINUE";Q8:PRINT:PRINT
5940 PRINT
5950 PRINT "**LONG-RANGE SCAN  ('3')"
5960 PRINT "    DISPLAYS INFORMATION ABOUT YOUR QUADRANT AND IT'S EIGHT"
5970 PRINT "IMMEDIATE NEIGHBORS:"
5980 PRINT "     103   8 104           (NUMBERS ARE INTERPRTETED"
5990 PRINT "     203 316   4            LIKE ON A GALACTIC MAP)"
6000 PRINT "     106 505 207"
6010 PRINT "THE COMPUTER UPDATES YOUR GALACTIC MAP AFTER EVERY LONG-RANGE"
6020 PRINT "SCAN."
6030 PRINT 
6040 PRINT "**PHASERS  ('4')"
6050 PRINT "    COMPUTER-CONTROLLED HIGH ENERGY BEAMS ARE FIRED AT ALL"
6060 PRINT "KLINGONS IN THE QUADRANT."
6070 PRINT 
6080 PRINT "PHOTON TORPEDOES  ('5')"
6090 PRINT "    ONE OF TEN TORPEDOES CAN BE FIRED AT A KLINGON. THEY MUST BE"
6100 PRINT "MANUALLY AIMED."
6105 PRINT:INPUT "HIT RETURN TO CONTINUE";Q8:PRINT:PRINT
6110 PRINT 
6120 PRINT "**SHIELDS  ('6')"
6130 PRINT "    AS THE KLINGONS HAVE WEAPONS LIKE YOURS, YOUR SHIP HAS"
6140 PRINT "SHIELDS TO PROTECT IT. THIS COMMAND CONTROLS THE ENERGY IN"
6150 PRINT "THE SHIELDS."
6160 PRINT 
6170 PRINT "**DAMAGE CONTROL  ('7')"
6180 PRINT "    ENABLES YOU TO CHECK THE OPERATIONAL STATUS OF ANY OF THE"
6190 PRINT "SHIP'S FUNCTIONS. "
6200 PRINT
6210 PRINT "**COMPUTER  ('8')"
6220 PRINT "    ACCESSES THE ON-BOARD COMPUTER."
6230 PRINT:PRINT
6235 PRINT:INPUT "HIT RETURN TO CONTINUE";Q8:PRINT:PRINT
6240 PRINT "    FOR A LIST OF AVAILABLE COMMANDS, TYPE A '0' WHEN ASKED FOR"
6250 PRINT "A COMMAND."
6260 PRINT 
6270 PRINT "    SOME FREE ADVICE FOR BEGINNERS:"
6280 PRINT "      1) PUT ENERY IN THE SHIELDS BEFORE WARPING."
6290 PRINT "      2) WHEN IN DOUBT, CONSULT THE COMPUTER (COMMAND 8)"
6300 PRINT "      3) CHOOSE A LOW SECURITY CLEARANCE AND A LOW RANK."
6310 PRINT 
6320 INPUT "HIT RETURN TO START";Q8
6330 RETURN
6340 PRINT "  AMOUNT TO TRANSFER FROM SHIELDS";:  INPUT Q8
6350 IF Q8<0  OR Q8>S  THEN  GOTO 6340
6360 S=S-Q8:E=E+Q8:PRINT
6370 PRINT E;" UNITS OF ENERGY NOW - TRY AGAIN."
6380 RETURN 
6390 RESET
8218 FROM 
